apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildToolsVersion

    def STRING = 'String'
    def AUTH_TOKEN = 'AUTH_TOKEN'
    def BASE_API_URL = 'BASE_API_URL'
    def BASE_RTF_URL = 'BASE_RTF_URL'

    defaultConfig {
        applicationId buildConfig.packageName

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode 1
        versionName "0.1.0"

        multiDexEnabled = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField STRING, AUTH_TOKEN, '"eyJhbGciOiJIUzI1NiJ9.eyJyZWZyZXNoYWJsZSI6ZmFsc2UsInN1YiI6ImJiMGNkYTJiLWExMGUtNGVkMy1hZDVhLTBmODJiNGMxNTJjNCIsImF1ZCI6ImJldGEuZ2V0YnV4LmNvbSIsInNjcCI6WyJhcHA6bG9naW4iLCJydGY6bG9naW4iXSwiZXhwIjoxODIwODQ5Mjc5LCJpYXQiOjE1MDU0ODkyNzksImp0aSI6ImI3MzlmYjgwLTM1NzUtNGIwMS04NzUxLTMzZDFhNGRjOGY5MiIsImNpZCI6Ijg0NzM2MjI5MzkifQ.M5oANIi2nBtSfIfhyUMqJnex-JYg6Sm92KPYaUL9GKg"'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(defaultConfig.applicationId + '.jks')
            storePassword 'android'
            keyAlias 'buxkeystore'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'api'

    productFlavors {
        development {
            // For debugging on the emulator; requires to run bux-server.jar on the local host
            dimension 'api'
            applicationIdSuffix '.dev'
            buildConfigField STRING, BASE_API_URL, '"http://10.0.2.2:8080/"'
            buildConfigField STRING, BASE_RTF_URL, '"http://10.0.2.2:8080/"'
        }

        beta {
            dimension 'api'
            applicationIdSuffix '.beta'
            buildConfigField STRING, BASE_API_URL, '"https://api.beta.getbux.com/"'
            buildConfigField STRING, BASE_RTF_URL, '"https://rtf.beta.getbux.com/"'
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    applicationVariants.all { variant ->
        variant.getCompileConfiguration().resolutionStrategy {
            force "com.android.support:support-v7:$versions.supportLibVersion"
            force "com.android.support:support-v4:$versions.supportLibVersion"
            force "com.android.support:design:$versions.supportLibVersion"
        }

        variant.getRuntimeConfiguration().resolutionStrategy {
            force "com.android.support:design:$versions.supportLibVersion"
        }
    }

    lintOptions {
        abortOnError = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    // Local libs
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlinVersion"

    // Support
    implementation "com.android.support:appcompat-v7:$versions.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$versions.supportLibVersion"
    implementation "com.android.support:cardview-v7:$versions.supportLibVersion"
    implementation "com.android.support:design:$versions.supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintLayoutVersion"
    implementation "androidx.core:core-ktx:$versions.ktxVersion"

    // Network
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$versions.retrofitVersion"
    implementation "com.squareup.moshi:moshi:$versions.moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshiVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofitVersion"
    implementation "com.github.tinder.scarlet:scarlet:$versions.scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-websocket-okhttp:$versions.scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-lifecycle-android:$versions.scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-message-adapter-moshi:$versions.scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-stream-adapter-rxjava2:$versions.scarletVersion"

    // Dependency injection
    implementation "javax.annotation:javax.annotation-api:$versions.javaAnnotationVersion"
    implementation "com.google.dagger:dagger:$versions.daggerVersion"
    implementation "com.google.dagger:dagger-android:$versions.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$versions.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$versions.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$versions.daggerVersion"

    // LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:$versions.lifecycleVersion"
    implementation "android.arch.lifecycle:viewmodel:$versions.lifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$versions.lifecycleVersion"

    // Data binding
    kapt "com.android.databinding:compiler:$versions.dataBindingVersion"

    // RX
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlinVersion"
    implementation "nl.littlerobots.rxlint:rxlint:$versions.rxLintVersion"

    // RX binding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxBindingVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"
    implementation "com.readystatesoftware.chuck:library:$versions.chuckVersion"
//    debugImplementation "com.readystatesoftware.chuck:library:$versions.chuckVersion"
//    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$versions.chuckVersion"

    // Utils
    implementation "com.jakewharton.threetenabp:threetenabp:$versions.threeTenAndroidVersion"

    // Multidex
    implementation "com.android.support:multidex:$versions.multidexVersion"

    // Test
    testImplementation "junit:junit:$versions.junitVersion"
    testImplementation "org.mockito:mockito-core:$versions.mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlinVersion"
    testImplementation "org.threeten:threetenbp:$versions.threeTenVersion"
    testImplementation "org.assertj:assertj-core:$versions.assertJVersion"

    // Android Test
    androidTestImplementation "com.android.support.test:runner:$versions.testRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espressoVersion"
}

ktlint {
    debug = true
    verbose = true
    android = true
    outputToConsole = true
    reporters = ["PLAIN"]
}
