apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion targetSdk.toInteger()
    buildToolsVersion buildToolsVer

    def STRING = 'String'
    def AUTH_TOKEN = 'AUTH_TOKEN'
    def BASE_API_URL = 'BASE_API_URL'
    def BASE_RTF_URL = 'BASE_RTF_URL'

    defaultConfig {
        applicationId "com.evastos.bux"

        minSdkVersion minSdk.toInteger()
        targetSdkVersion targetSdk.toInteger()

        versionCode 1
        versionName "0.1.0"

        multiDexEnabled = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField STRING, AUTH_TOKEN, '"eyJhbGciOiJIUzI1NiJ9.eyJyZWZyZXNoYWJsZSI6ZmFsc2UsInN1YiI6ImJiMGNkYTJiLWExMGUtNGVkMy1hZDVhLTBmODJiNGMxNTJjNCIsImF1ZCI6ImJldGEuZ2V0YnV4LmNvbSIsInNjcCI6WyJhcHA6bG9naW4iLCJydGY6bG9naW4iXSwiZXhwIjoxODIwODQ5Mjc5LCJpYXQiOjE1MDU0ODkyNzksImp0aSI6ImI3MzlmYjgwLTM1NzUtNGIwMS04NzUxLTMzZDFhNGRjOGY5MiIsImNpZCI6Ijg0NzM2MjI5MzkifQ.M5oANIi2nBtSfIfhyUMqJnex-JYg6Sm92KPYaUL9GKg"'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(defaultConfig.applicationId + '.jks')
            storePassword 'android'
            keyAlias 'buxkeystore'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'api'

    productFlavors {
        development {
            // Only for debugging using the emulator, run bux-server.jar on your local host
            dimension 'api'
            applicationIdSuffix '.dev'
            buildConfigField STRING, BASE_API_URL, '"http://10.0.2.2:8080/"'
            buildConfigField STRING, BASE_RTF_URL, '"http://10.0.2.2:8080/"'
        }

        beta {
            dimension 'api'
            applicationIdSuffix '.beta'
            buildConfigField STRING, BASE_API_URL, '"https://api.beta.getbux.com/"'
            buildConfigField STRING, BASE_RTF_URL, '"https://rtf.beta.getbux.com/"'
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    applicationVariants.all { variant ->
        variant.getCompileConfiguration().resolutionStrategy {
            force "com.android.support:support-v7:$supportLibVersion"
            force "com.android.support:support-v4:$supportLibVersion"
            force "com.android.support:design:$supportLibVersion"
        }

        variant.getRuntimeConfiguration().resolutionStrategy {
            force "com.android.support:design:$supportLibVersion"
        }
    }

    lintOptions {
        abortOnError = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    // Local libs
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Support
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"

    // Network
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.github.tinder.scarlet:scarlet:$scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-websocket-okhttp:$scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-lifecycle-android:$scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-message-adapter-moshi:$scarletVersion"
    implementation "com.github.tinder.scarlet:scarlet-stream-adapter-rxjava2:$scarletVersion"

    // Dependency injection
    implementation "javax.annotation:javax.annotation-api:$javaAnnotationVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"
    implementation "android.arch.lifecycle:viewmodel:$lifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$lifecycleVersion"

    // Data binding
    kapt "com.android.databinding:compiler:$dataBindingVersion"

    // RX
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "nl.littlerobots.rxlint:rxlint:$rxLintVersion"

    // RX binding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.readystatesoftware.chuck:library:$chuckVersion"
//    debugImplementation "com.readystatesoftware.chuck:library:$chuckVersion"
//    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuckVersion"

    // Utils
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenAndroidVersion"

    // Multidex
    implementation "com.android.support:multidex:$multidexVersion"

    // Test
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.threeten:threetenbp:$threeTenVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"

    // Android Test
    androidTestImplementation "com.android.support.test:runner:$testRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
}

ktlint {
    debug = true
    verbose = true
    android = true
    outputToConsole = true
    reporters = ["PLAIN"]
}
